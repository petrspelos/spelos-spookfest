// ============================================================================
#include "AmnesiaSignatures.cpp"
#include "PlayMusicExtensions.ihps"
#include "ChangeMapExtensions.ihps"
#include "PlaySoundAtEntityExtensions.ihps"
#include "SanityBeat.ihps"

void OnEnter()
{
    Preload();
    PlayMusic("TrippingWithSpelos.ogg");
    
    SetPlayerActive(false);
    FadeOut(0.0f);
    SetLightVisible("SpotLight_1", false);

    PlaySoundAtEntity("SkeletonRidePanic.snt", 0.5f);
    PlaySoundAtEntity(
        "wind",           /* SoundName */
        "FallingAmb.snt", /* SoundFile */
        "Player",         /* Entity */
        0.5f,             /* FadeTime */
        false             /* SaveSound */
    );
    PlaySoundAtEntity(
        "elevator",       /* SoundName */
        "elevator_move_fall_quieter.snt", /* SoundFile */
        "Player",         /* Entity */
        0.5f,             /* FadeTime */
        false             /* SaveSound */
    );

    AddTimer("", 9.5f, "StartTrip");
    AddTimer("", 59.0f, "EndTrip");
    AddTimer("", 65.0f, "Explode");
    AddTimer("", 70.0f, "NextMap");
}

void StartTrip(string t)
{
    FadeIn(0.2f);
    
    SetSanityBeatFov(0.8f);
    SetSanityBeatCorrectAfterSeconds(0.3f);
    SetSanityBeatRepeatInSeconds(0.75f);
    StartSanityBeat();
    StartHeadRotation();

    AddTimer("", 0.5f, "SanityBeat");
    SetLightVisible("SpotLight_1", true);
    StartScreenShake(0.03f, 60.0f, 0.0f, 0.0f);
    FadeRadialBlurTo(0.02f, 0.05f);
    PlayPropAnimation("Ride", "Ride", 0.0f, true, "");
    FadePlayerFOVMulTo(1.2f, 1.0f);
    SetSanityDrainDisabled(true);
}

void StartHeadRotation()
{
    HeadRotationLoop("");
}

void HeadRotationLoop(string t)
{
    if(GetLocalVarInt("StopRotation") == 1) return;
    float rotation = RandFloat(-20.0f, 20.0f);
    FadePlayerRollTo(rotation, 20.0f, 50.0f);
    AddTimer("", RandFloat(0.5f, 1.5f), "HeadRotationLoop");
}

void OnLeave()
{
    FadePlayerRollTo(0.0f, 10.0f, 20.0f);
}

void EndTrip(string t)
{
    SetLocalVarInt("StopRotation", 1);
    FadeOut(1.5f);
    StopSound("elevator", 1.0f);
    StopSanityBeat();
}

void Explode(string t)
{
    PlaySoundAtEntity("", "explosion_rock_large.snt", "Player", 0.0f, false);
    StopSound("wind", 0.5f);
}

void NextMap(string t)
{
    ChangeMap("sphw_06_Hell.map");
}
